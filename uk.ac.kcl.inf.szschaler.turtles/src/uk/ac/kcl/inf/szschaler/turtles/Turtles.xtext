grammar uk.ac.kcl.inf.szschaler.turtles.Turtles with org.eclipse.xtext.common.Terminals

generate turtles "http://www.ac.uk/kcl/inf/szschaler/turtles/Turtles"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

TurtleProgram:
	statements += Statement*
;
	
Statement:
	MoveStatement |
	TurnStatement |
	LoopStatement |
	PenMoveStatement |
	VariableDeclaration
;

PenMoveStatement:
	'pen' state=PenState
;

enum PenState:
	up | down
;

VariableDeclaration:
	"var" name=ID "=" value=INT
;

LoopStatement:
	count = Addition "times" "do"
	  statements += Statement+
	"end"
;

MoveStatement:
	command = MoveCommand "(" steps = Addition ")"
;

enum MoveCommand:
	forward | backward
;

TurnStatement:
	"turn" command = TurnCommand "by" degrees = Addition "degrees"
;

enum TurnCommand:
	left | right
;

Addition returns Expression:
	Multiplication ({Addition.left = current} operator+=("+"|"-") right+=Multiplication)*
;

Multiplication returns Expression:
	Primary ({Multiplication.left = current} operator+=("*"|"/") right+=Primary)*
;

Primary returns Expression:
	IntLiteral |
	IntVarExpression |
	RealLiteral |
	"(" Addition ")"
;

IntLiteral:
	val = INT
;

RealLiteral:
	val = REAL
;

IntVarExpression:
	var = [VariableDeclaration]
;

REAL returns ecore::EFloat hidden():
	INT? "." INT
; 